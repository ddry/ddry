###
# Command line interface logger
###

'use strict'

c = require '../common/colors'
usage = require './usage'

errors =
  codeFolderUndefined: ->
    'Code folder definition missing'
  configurerMissing: (path) ->
    "Failed to load configurer file from #{c.bright path}"
  configurerUndefined: (path) ->
    "In order to omit configurer path please specify it at least once."
  noCodeFolder: (code) ->
    "Specified code folder #{c.bright code} does not exist."
  noConfigFound: ->
    "No spec config file (#{c.bright 'ddry.json'}) found. Run 'ddry init' to create it first."
  usage: (command) ->
    usage[command]

messages =
  configured: (p) ->
    "Config object generated by #{c.bright p.path} for '#{p.params}'"
  noTitle: (filePath) ->
    "No title defined in #{c.bright filePath}"
  titleExtracted: (p) ->
    "Extracted #{c.green p.title} from #{c.bright p.filePath}"
  unscoped: ->
    "Running spec with unscoped #{c.bright 'ddry.json'}"

module.exports =
  error: (alias, param, calm = false) ->
    badge = if calm then '' else c.red 'ERROR:'
    throw new Error "#{badge} #{errors[alias] param}"

  info: (alias, param) ->
    console.log "#{messages[alias] param}"
